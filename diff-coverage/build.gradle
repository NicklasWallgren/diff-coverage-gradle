plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.5"
    id 'jacoco'
}

group 'com.form.coverage'
version '0.5.2'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile project(':jacoco-filtering-extension')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mock-server:mockserver-netty:3.10.8'
    testCompile 'org.mock-server:mockserver-client-java:3.10.8'
}

test {
    useJUnitPlatform()
}

sourceSets {
    functionalTest {
        kotlin.srcDir file('src/funcTest/kotlin')
        resources.srcDir file('src/funcTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    dependsOn += generateJacocoTestKitProperties
}
generateJacocoTestKitProperties.destinationFile=file("$buildDir.absolutePath/jacoco/integration.exec")

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

check.dependsOn functionalTest
