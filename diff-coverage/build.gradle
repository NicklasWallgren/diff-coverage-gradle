plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.4"
    id 'jacoco'
}

group 'com.worldapp.coverage'
version '0.3.1'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile project(':jacoco-filtering-extension')

    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.worldapp.coverage'
            version = '0.3'

            artifactId = 'diff-coverage'
            from components.java
        }
    }
}

sourceSets {
    functionalTest {
        kotlin.srcDir file('src/funcTest/kotlin')
        resources.srcDir file('src/funcTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

check.dependsOn functionalTest