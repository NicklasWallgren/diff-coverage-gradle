buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://nexus.t1.tenet/nexus/content/repositories/public/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.worldapp.coverage:diff-coverage:0.4.3"
    }
}

group 'com.worldapp'

subprojects{
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'com.worldapp.diff-coverage'

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile 'org.jacoco:org.jacoco.core:0.8.4'
        compile 'org.jacoco:org.jacoco.report:0.8.4'
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'

        testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
        testImplementation "io.mockk:mockk:1.9.3.kotlin12"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    sourceCompatibility = 1.8

    def subProjectName=it.name
    publishing {
        publications {
            "${subProjectName}"(MavenPublication) {
                from components.java
            }
        }
        if (project.hasProperty('nexusRepo')) {
            repositories {
                maven {
                    name 'nexus'
                    url "$nexusRepo/content/repositories/releases/"
                    credentials {
                        username = nexusUsername
                        password = nexusPassword
                    }
                }
            }
        }
    }

    jacoco {
        toolVersion = "0.8.3"
    }

    jacocoTestReport {
        reports {
            html.enabled = true
        }
        executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    }

    diffCoverageReport {
        diffFile = project.hasProperty('diffFilePath') ? diffFilePath : "<NOT SPECIFIED>"
        reports {
            html = true
        }

        violationRules {
            minBranches = 1
            minLines = 1
            minInstructions = 1
            failOnViolation = true
        }
    }
}

